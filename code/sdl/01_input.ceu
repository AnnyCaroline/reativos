#include "sdl.ceu"

native do
    ##include <assert.h>
end

input void SDL_REDRAW;
input void SDL_QUIT;
input _SDL_KeyboardEvent*    SDL_KEYDOWN;
input _SDL_MouseButtonEvent* SDL_MOUSEBUTTONDOWN;

/* INITIALIZATION */

var _SDL_Window* window;
    finalize
        window = _SDL_CreateWindow("Hello World!",
                    _SDL_WINDOWPOS_UNDEFINED, _SDL_WINDOWPOS_UNDEFINED,
                    640, 480, _SDL_WINDOW_SHOWN
                 );
    with
        _SDL_DestroyWindow(window);
    end
    _assert(window != null);

var _SDL_Renderer* renderer;
    finalize
        renderer = _SDL_CreateRenderer(window, -1, 0);
    with
        _SDL_DestroyRenderer(renderer);
    end
    _assert(renderer != null);

/* EXECUTION */

var _SDL_Rect r;
    r.x = 200;
    r.y = 200;
    r.w =  50;
    r.h =  50;

par/or do
    await SDL_QUIT;
with
    var _SDL_KeyboardEvent* key;
    every key in SDL_KEYDOWN do
        if key:keysym.sym == _SDLK_UP then
            r.y = r.y - 10;
        else/if key:keysym.sym == _SDLK_DOWN then
            r.y = r.y + 10;
        else/if key:keysym.sym == _SDLK_LEFT then
            r.x = r.x - 10;
        else/if key:keysym.sym == _SDLK_RIGHT then
            r.x = r.x + 10;
        end
    end
with
    every SDL_REDRAW do
        _SDL_SetRenderDrawColor(renderer, 0xFF,0xFF,0xFF,0x00);
        _SDL_RenderFillRect(renderer, null);

        _SDL_SetRenderDrawColor(renderer, 0x00,0x00,0xFF,0x00);
        _SDL_RenderFillRect(renderer, &r);

        _SDL_RenderPresent(renderer);
    end
end

escape 0;
