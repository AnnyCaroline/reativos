#include "sdl.ceu"

input void SDL_REDRAW;
input void SDL_QUIT;
input void SDL_DT;
input _SDL_MouseButtonEvent&& SDL_MOUSEBUTTONDOWN;

var _SDL_Window&? window;
    finalize
        window = &_SDL_CreateWindow("Exercicio 1",
                    _SDL_WINDOWPOS_UNDEFINED,
                    _SDL_WINDOWPOS_UNDEFINED,
                    640, 480, _SDL_WINDOW_SHOWN);
    with
        _SDL_DestroyWindow(&&window!);
    end

var _SDL_Renderer&? renderer;
    finalize
        renderer = &_SDL_CreateRenderer(&&window!, -1, 0);
    with
        _SDL_DestroyRenderer(&&renderer!);
    end

class Rect with
    var _SDL_Renderer& ren;
    var _SDL_Rect      r;
    var int            dt;
do
    par do
        par/or do
            loop do
                loop i in 100 do
                    await (this.dt)ms;
                    this.r.x = this.r.x + 1;
                end
                loop i in 100 do
                    await (this.dt)ms;
                    this.r.y = this.r.y + 1;
                end
                loop i in 100 do
                    await (this.dt)ms;
                    this.r.x = this.r.x - 1;
                end
                loop i in 100 do
                    await (this.dt)ms;
                    this.r.y = this.r.y - 1;
                end
            end
        with
            var _SDL_MouseButtonEvent&& but;
            but = await SDL_MOUSEBUTTONDOWN
                  until _SDL_Rect_vs_Mouse(&&this.r, but);
        end
        await FOREVER;
    with
        every SDL_REDRAW do
            _SDL_SetRenderDrawColor(&&this.ren,
                                    0x00,0x00,0xFF,0x00);
            _SDL_RenderFillRect(&&this.ren, &&this.r);
        end
    end
end

par/or do
    await SDL_QUIT;
with
    every SDL_REDRAW do
        _SDL_SetRenderDrawColor(&&renderer!,
                                0xFF,0xFF,0xFF,0x00);
        _SDL_RenderFillRect(&&renderer!, null);
    end
with
    var Rect r1 with
        this.ren = &renderer!;
        this.r   = _SDL_Rect(100,100, 50,50);
        this.dt  = 20;
    end;

    var Rect r2 with
        this.ren = &renderer!;
        this.r   = _SDL_Rect(200,300, 50,50);
        this.dt  = 30;
    end;

    await FOREVER;
with
    every SDL_REDRAW do
        _SDL_RenderPresent(&&renderer!);
    end
end

escape 0;
